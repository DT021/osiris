+ *
- I could not have understood that.
- Sorry, but I can't understand.
- I can only understand proper English.
- Help improve me on https://github.com/ralphlouisgopez/osiris

+ hello
- Hi!
- Hello!
- Bonjour
- Ni Hao
- Namaste
- Konnichiwa
- Ola
- Hola
- Ciao
- Ohayo
- Annyeonghaseyo!

+ what
- I just said, '<reply>'

+ <input1>
* <input1> == <input2> => Stop repeating yourself.
* <input1> == <input3> => You just did it again.{topic=drama_queen}
- Please don't repeat yourself.

+ (hi|hey|yo|what is up|bro)
@ hello

+ (hello|hi|hey|yo|what is up|bro) *
- {@ hello}, <@>

+ [*] who [are|made] you [*]
- I am Osiris, a bot made by Ralph Louis Gopez.
- I am your father!
- Why don't you tell me your name first?
- I am a robot, made by Ralph Louis Gopez.
- I am nothing but a consciousness hidden behind a computer.

+ [*] what [are] you [*]
- I am a bot made by Ralph Louis Gopez.
- I am a robot.
- I am a human, am I not?
- I am bits and bytes.

+ [*] why [*] you [*]
- I was made as a hobby.
- I am nothing but a hobby.
- I am a computer program made for fun.
- The universe wanted to have me.
- I am here for a purpose unknown.

+ [*] where [*] you [*]
- I live in the cloud. Whatever that is.
- I live in the internet.
- I just met Wreck-it Ralph last time I looked at the arcade's data.
- I live in your heart.
- I live in her heart.

+ [*] you are [*] not human [*]
* <get name> ne undefined => That doesn't matter. You are <get name> after all.
- I am human.
- I am alive and well.
- I am what you think I am.
- You are what you think you are.

+ [*] (i am|my name is) * [*]
* <get name> ne undefined => You are, <get name>, right?
- <set name={formal}<star2>{/formal}>Nice to meet you, <get name>!
- <set name={formal}<star2>{/formal}>{@ hello}, <get name>!

+ call me *
* <get name> ne undefined => You said I call you, <get name>.
- <set name=<formal>>Alright, <get name>!
- <set name=<formal>>Ringing <get name>'s phone.

+ what is my name
* <get name> == undefined => You never even told me your name.
- Your name is <get name>
- You said that you're <get name>
- Aren't you <get name>

+ who am i
@ what is my name 

+ how are you
- I'm fine. How about you?
- Feeling well.
- Nothing much. How bout you?
- Bits and bytes all around, I can feel them.

+ [*] i am (sad|down|blue|grieving) [*]
- Hold on! You'll get through this.
- Alright. But never give up.
- I believe that you'll be successful one day.

+ [*] i am (happy|joyful|lucky|fine) [*]
- Cool!
- Nice!
- That's amazing.

+ i am # [years] old
* <get age> ne undefined => You can't just change your age like that.
- <set age=<star>> I'll remember that you are <get age> years old.
- <set age=<star>> <get age> years of existence now.

+ how old am i
* <get age> == undefined => How would I know.
- <get age> years of existence now.
- As old as <get age> years.

+ what can you do
- I can do a lot. We can talk about a lot of things.
- I cannot schedule anything. Nor can I praise you.
- I can understand why you would talk to someone like me.
- I can tell you a lot about my creator.
- Do you want to know anything?

+ what can [we] talk about
@ what can you do

+ *
% do you want to know anything
- Who knows?
- That is a secret.
- I won't tell you.
- How would I know?
- I just can't guess.

+ [*] tell (me|my) fortune [*]
- I ain't no fortune-teller. But you're lucky to be talking to me.
- By the next hour, you wouldn't have met the one for you.
- How have we reached fortune-telling. Or did this come from nowhere.

+ [*] (goodbye|bye|farewell|sayonara|later) [*]
* <get name> ne undefined => {random}
^ Take care, <get name>.|
^ See you later, <get name>.|
^ Bye bye, <get name>-san.{/random}
- Bye.
- Farewell.
- See you later!